PREFIX schema: <https://schema.org/>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbpage:  <https://dbpedia.org/page/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

# Medal won by a member of the IOC Refugee Olympic Team
SELECT ?name ?medal_type ?name_event WHERE {
	?delegation rdf:type ex:delegation ;
             schema:name "IOC Refugee Olympic Team at the 2024 Summer Olympics" .
  	?medalist rdf:type foaf:Person ;
      		foaf:name ?name ;
        	ex:delegation ?delegation ;
  			ex:won_medal ?medal .
  	?medal ex:medal_type ?medal_type ;
         ex:won_at/schema:name ?name_event .
}

# Medalists with the most medal
SELECT ?name_medalist (COUNT(?medal) AS ?nb_medal) WHERE {
	?medal rdf:type ex:medal ;
        ex:medal_type ?medal_type .
    ?medalist ex:won_medal ?medal ;
              foaf:name ?name_medalist .
} GROUP BY ?medalist ?name_medalist
ORDER BY DESC(?nb_medal)
LIMIT 25

# Medalists with the most medal, with details of the number of gold, silver and bronze medals
SELECT ?medalist_name 
    (STR(COUNT(?medal_type)) AS ?total_medals)	# Use STR() to hide the ^^<http://www.w3.org/2001/XMLSchema#integer>
	(STR(SUM(IF(?medal_type = "gold", 1, 0))) AS ?nb_gold)  # type annotation in the result
	(STR(SUM(IF(?medal_type = "silver", 1, 0))) AS ?nb_silver)
	(STR(SUM(IF(?medal_type = "bronze", 1, 0))) AS ?nb_bronze)
WHERE {
  ?medalist foaf:name ?medalist_name ;
            ex:won_medal ?medal .
  ?medal ex:medal_type ?medal_type .
}
GROUP BY ?medalist_name
ORDER BY DESC(?total_medals) DESC(?nb_gold) DESC(?nb_silver) DESC(?nb_bronze)
LIMIT 10

# Total number of medals per country
SELECT ?country_name (COUNT(?medal) AS ?nb_medal) WHERE {
	?medal rdf:type ex:medal ;
        ex:medal_type ?medal_type .
    ?medalist ex:won_medal ?medal ;
              dbo:country ?country .
  	?country schema:name ?country_name .
} GROUP BY ?country ?country_name
ORDER BY DESC(?nb_medal)
LIMIT 25

# Total number of medals per country, with details of the number of gold, silver and bronze medals
SELECT ?country_name (STR(?total_medals) AS ?total_medal_str) (STR(?nb_gold) AS ?nb_gold_str) (STR(?nb_silver) AS ?nb_silver_str) (STR(?nb_bronze) AS ?nb_bronze_str) WHERE {
  # Use STR() to hide the ^^<http://www.w3.org/2001/XMLSchema#integer> type annotation in the result
  SELECT ?country_name 
    (COUNT(?medal_type) AS ?total_medals)	
    (SUM(IF(?medal_type = "gold", 1, 0)) AS ?nb_gold)
    (SUM(IF(?medal_type = "silver", 1, 0)) AS ?nb_silver)
    (SUM(IF(?medal_type = "bronze", 1, 0)) AS ?nb_bronze)
  WHERE {
    ?medalist foaf:name ?medalist_name ;
              ex:won_medal ?medal ;
              dbo:country ?country .
    ?country schema:name ?country_name .
    ?medal ex:medal_type ?medal_type .
  }
  GROUP BY ?country_name
  ORDER BY DESC(?total_medals) DESC(?nb_gold) DESC(?nb_silver) DESC(?nb_bronze)
  LIMIT 10
}

# Number of medal per region
SELECT ?nuts2_name (COUNT(?medal) AS ?nb_medal) WHERE {
	?medal rdf:type ex:medal .
    ?medalist ex:won_medal ?medal ;
              dbo:birthPlace ?birth_place .
  	?birth_place ex:nut2_name ?nuts2_name .
  MINUS {?birth_place ex:nut2_name "NA"}
} 
GROUP BY ?nuts2_name
ORDER BY DESC(?nb_medal)
LIMIT 5