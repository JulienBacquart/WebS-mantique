PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbpage: <http://dbpedia.org/page/>
PREFIX ex: <http://example.org/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema>
PREFIX schema: <http://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
    ?medalist_id a foaf:Person;
                rdfs:seeAlso        ?medalist_link;        
                foaf:name           ?medalist_name;
                dbo:birthDate       ?date_of_birth_typed;
                foaf:gender         ?sex_or_gender;
                dbo:birthPlace      ?place_of_birth_id;
                ex:delegation       ?delegation_id;
                # ex:won_medal        ?medal_id;  # See later in the file
                dbo:sport           ?sport;
                dbo:country         ?country_id.

    ?delegation_id a ex:delegation;
                    rdfs:seeAlso    ?delegation_link;
                    schema:name     ?delegation_name.

    ?country_id a dbo:country;
                    schema:name       ?country_medal;
                    dbpage:ISO_3166-1_alpha-2   ?country_medal_code2; # keep only one and use dbo:iso31661Code ?
                    dbpage:ISO_3166-1_alpha-3   ?country_medal_code3.
    
    # Need to be physically separated from where the rest of medalist is definied, otherwise TARQL group the medal together
    # and drop them when using the --dedup option (?!)
    ?medalist_id    ex:won_medal   ?medal_id.

    ?medal_id a ex:medal;
            ex:medal_type   ?medal;
            ex:won_at       ?event_id.

    ?event_id a schema:Event; # Find a prefix specialy for sport event ?
            rdfs:seeAlso    ?event_link;
            schema:name     ?event_name.

    ?place_of_birth_id a schema:Place;
        schema:name     ?place_of_birth;
        geo:lat         ?lat_typed;
        geo:long        ?lon_typed;
        ex:nuts0_id      ?nuts0_id_uri;
        ex:nuts0_name    ?nuts0_name_fixed;
        ex:nuts1_id      ?nuts1_id_uri;
        ex:nuts1_name    ?nuts1_name_fixed;
        ex:nuts2_id      ?nuts2_id_uri;
        ex:nuts2_name    ?nuts2_name_fixed;
        ex:nuts2_population  ?nuts2_population_typed;
        ex:nuts2_gdp     ?nuts2_gdp_typed; # Will be missing if NA
        ex:nuts3_id      ?nuts3_id_uri;
        ex:nuts3_name    ?nuts3_name_fixed;
        ex:nuts3_population  ?nuts3_population_typed;
        ex:nuts3_gdp     ?nuts3_gdp_typed. # Will be missing if NA

    # ?sport_id a dbo:Sport; # Remove and include directly into event ?
    #         schema:name       ?sport.
}
# FROM <file:./dataset/2024_medalists_nuts_only.csv#encoding=utf-8;header=present;delimiter=comma>
WHERE {
    # Medalist
    BIND (URI(CONCAT("http://www.wikidata.org/entity/", ?medalist_wikidata_id)) AS ?medalist_id).
    BIND (xsd:date(?date_of_birth) AS ?date_of_birth_typed).

    # Place of birth
    BIND (IF(?place_of_birth_wikidata_id != 'NA', URI(CONCAT("http://www.wikidata.org/entity/", ?place_of_birth_wikidata_id)), ?nothing) AS ?place_of_birth_id) .

    # Latitude and longitude
    BIND (xsd:float(?lat) AS ?lat_typed).
    BIND (xsd:float(?lon) AS ?lon_typed).
    
    # Nuts 0
    BIND (IF(?nuts0_id != 'NA', URI(CONCAT("http://data.europa.eu/nuts/code/", ?nuts0_id)), ?nothing) AS ?nuts0_id_uri) .
    BIND (IF(?nuts0_name != 'NA', ?nuts0_name, ?nothing) AS ?nuts0_name_fixed) .

    # Nuts 1
    BIND (IF(?nuts1_id != 'NA', URI(CONCAT("http://data.europa.eu/nuts/code/", ?nuts1_id)), ?nothing) AS ?nuts1_id_uri) .
    BIND (IF(?nuts1_name != 'NA', ?nuts1_name, ?nothing) AS ?nuts1_name_fixed) .

    # Nuts 2
    BIND (IF(?nuts2_id != 'NA', URI(CONCAT("http://data.europa.eu/nuts/code/", ?nuts2_id)), ?nothing) AS ?nuts2_id_uri) .
    BIND (IF(?nuts2_name != 'NA', ?nuts2_name, ?nothing) AS ?nuts2_name_fixed) .

    # Nuts 3
    BIND (IF(?nuts3_id != 'NA', URI(CONCAT("http://data.europa.eu/nuts/code/", ?nuts3_id)), ?nothing) AS ?nuts3_id_uri) .
    BIND (IF(?nuts3_name != 'NA', ?nuts3_name, ?nothing) AS ?nuts3_name_fixed) .

    BIND (xsd:int(?nuts2_population) AS ?nuts2_population_typed).
    BIND (xsd:float(?nuts2_gdp) AS ?nuts2_gdp_typed).   # If NA will be dropped
    BIND (xsd:int(?nuts3_population) AS ?nuts3_population_typed).
    BIND (xsd:float(?nuts3_gdp) AS ?nuts3_gdp_typed).   # If NA will be dropped

    # Delegation
     BIND (URI(CONCAT("http://www.wikidata.org/entity/", ?delegation_wikidata_id)) AS ?delegation_id).

    # Country
    BIND (IF(?country_medal_wikidata_id != 'NA', URI(CONCAT("http://www.wikidata.org/entity/", ?country_medal_wikidata_id)), ?nothing) AS ?country_id) .

    # Medal
    BIND (URI(CONCAT('medal_', STR(?ROWNUM))) AS ?medal_id).

    # Event
    BIND (URI(CONCAT("http://www.wikidata.org/entity/", ?event_wikidata_id)) AS ?event_id).

    # # Meta-Event (?!)
    # BIND (URI(CONCAT("http://www.wikidata.org/entity/", ?event_part_of_wikidata_id)) AS ?meta_event_id).

    # # Sport
    # BIND (URI(CONCAT("http://www.wikidata.org/entity/", ?sport_wikidata_id)) AS ?sport_id).    
}

# SELECT (COUNT(*) AS ?count)
#   FROM <./dataset/2024_medalists_nuts_only.csv#encoding=utf-8;header=present;delimiter=comma>
#   WHERE {}